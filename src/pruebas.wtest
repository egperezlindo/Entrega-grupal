import menus.*
import config.*
import musica.*
import niveles.*
import visuales.*
import corazones.*
import movimientos.*
import proyectiles.*
import wollok.game.*

describe "Tests de menus" {
    method initialize() { juegoPorNiveles.iniciarJuego() }
    
    test "No deja abrir la pausa en el menu de inicio" {
        assert.that(menuInicio.abierto())
        menuPausa.abrir()
        assert.notThat(menuPausa.abierto())
    }
    test "No deja abrir la ventana de controles si el menu de pausa no esta abierto" {
        menuInicio.cerrar()
        juegoPorNiveles.nivelActual().pantalla().cerrar()
        assert.notThat(menuPausa.abierto())
        menuControles.abrir()
        assert.notThat(menuControles.abierto())
    }
    test "La pausa se abre y cierra normalmente" {
        menuInicio.cerrar()
        juegoPorNiveles.nivelActual().pantalla().cerrar()
        assert.notThat(menuPausa.abierto())
        menuPausa.abrir()
        assert.that(menuPausa.abierto())
        menuPausa.abrir()
        assert.notThat(menuPausa.abierto())
    }
    test "La ventana de controles se abre y cierra normalmente" {
        menuInicio.cerrar()
        juegoPorNiveles.nivelActual().pantalla().cerrar()
        menuPausa.abrir()
        assert.notThat(menuControles.abierto())
        menuControles.abrir()
        assert.that(menuControles.abierto())
        menuControles.abrir()
        assert.notThat(menuControles.abierto())
    }
    test "Se puede volver al menu de inicio desde la pausa" {
        menuInicio.cerrar()
        juegoPorNiveles.nivelActual().pantalla().cerrar()
        menuPausa.abrir()
        juegoPorNiveles.nivelActual().volverAlMenu()
        assert.that(menuInicio.abierto())
    }

    test "No se puede abrir el menu de pausa en una pantalla de carga" {
        menuInicio.cerrar()
        assert.notThat(menuPausa.abierto())
        menuPausa.abrir()
        assert.notThat(menuPausa.abierto())
    }

    test "La musica funciona en cada nivel" {
        menuInicio.cerrar()
        juegoPorNiveles.nivelActual().pantalla().cerrar()
        assert.that(juegoPorNiveles.nivelActual().musica().estado())
    }

    test "La musica se detiene al volver al menu" {
        menuInicio.cerrar()
        juegoPorNiveles.nivelActual().pantalla().cerrar()
        menuPausa.abrir()
        juegoPorNiveles.nivelActual().volverAlMenu()
        assert.notThat(juegoPorNiveles.nivelActual().musica().estado())
    }
    
}

describe "Grupo de tests de niveles" {
    method initialize() {juegoPorNiveles.iniciarJuego()}
    test "El primer nivel al iniciar es el nivel uno" { 
        assert.equals(nivelUno, juegoPorNiveles.nivelActual()) 
    }
    test "Ahora el nivel es el dos por el cambio de índice" {
        juegoPorNiveles.indice(1)
        assert.equals(nivelDos, juegoPorNiveles.nivelActual()) 
    }
    test "Ahora el nivel es el dos porque el nivel uno ya se ganó, el gusano murió" {
        gusano.perderVida()
        gusano.perderVida()
        gusano.perderVida()
        assert.that(gusano.estaMuerto())
        assert.that(nivelUno.estaGanado())
        assert.equals(nivelDos, juegoPorNiveles.nivelActual()) 
    }
    test "Ahora es el nivel tres por el cambio de índice"{
        juegoPorNiveles.indice(2)
        assert.equals(nivelTres, juegoPorNiveles.nivelActual())
    }
}